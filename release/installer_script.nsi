; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Jetbrains Launcher"
!define PRODUCT_VERSION "v1.0.0"
!define PRODUCT_PUBLISHER "Minsuk Eom"
!define PRODUCT_WEB_SITE "https://github.com/BoxResin/Jetbrains-Launcher"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

Var PythonPath
Var IntelliJPath
Var AndroidStudioPath
Var PycharmPath

SetCompressor lzma

; MUI 1.67 compatible ------
!include "MUI.nsh"
!include "nsdialogs.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_CUSTOMFUNCTION_UNGUIINIT "un.myUnOnGuiInit"
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\orange-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\orange-uninstall.ico"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; Path input page
;Page custom PathInput
Page custom fnc_path_input_Show PathInputLeave
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "Korean"
!insertmacro MUI_LANGUAGE "English"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; Load language & custom dialog resources.
!include "lang.nsh"
!include "path_input.nsdinc"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Setup.exe"
InstallDir "$PROGRAMFILES\Jetbrains Launcher"
ShowInstDetails show
ShowUnInstDetails show

; Show the language choise dialog.
Function .onInit
  !insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

;Var Dialog
;Function PathInput
;  fnc_path_input_Show
  ;!insertmacro MUI_HEADER_TEXT $(PAGE_PATH_INPUT_TITLE) $(PAGE_PATH_INPUT_SUBTITLE)
  ;!insertmacro MUI_INSTALLOPTIONS_EXTRACT "option.ini"
  ;!insertmacro MUI_INSTALLOPTIONS_DISPLAY "option.ini"
  
;FunctionEnd

Function PathInputLeave
  ${NSD_GetText} $hCtl_path_input_PythonInterpreter_Txt $PythonPath
  ${NSD_GetText} $hCtl_path_input_IntelliJ_Txt $IntelliJPath
  ${NSD_GetText} $hCtl_path_input_AndroidStudio_Txt $AndroidStudioPath
  ${NSD_GetText} $hCtl_path_input_Pycharm_Txt $PycharmPath
FunctionEnd

Section "MainSection" SEC01
  ; Overwrite if same files exist.
  SetOverwrite on
  
  ; Things to install
  SetOutPath "$INSTDIR\"
  File "icon.ico"
  
  ; Create run.bat file.
  FileOpen $4 "run.bat" w
  FileWrite $4 '@echo off$\nset PYTHONPATH=$INSTDIR $\n"$PythonPath" "$INSTDIR\script\jetbrains_launcher.py" %1'
  FileClose $4
  
  ; Write ini file.
  WriteINIStr ".\prefs.ini" "Path" "PYTHON" $PythonPath
  WriteINIStr ".\prefs.ini" "Path" "INTELLIJ" $IntelliJPath
  WriteINIStr ".\prefs.ini" "Path" "ANDROID_STUDIO" $AndroidStudioPath
  WriteINIStr ".\prefs.ini" "Path" "PYCHARM" $PycharmPath
  WriteINIStr ".\prefs.ini" "Others" "LANGUAGE" $LANGUAGE
  FlushINI ".\prefs.ini"
  
  SetOutPath "$INSTDIR\language"
  File "..\language\*"
  
  SetOutPath "$INSTDIR\script"
  File "..\script\*"
  
  ; Write registries.
  WriteRegStr HKCR "Directory\shell\JetbrainLauncher" "" $(MENU_TITLE)
  WriteRegStr HKCR "Directory\Background\shell\JetbrainLauncher" "" $(MENU_TITLE)
  WriteRegStr HKCR "Directory\shell\JetbrainLauncher" "Icon" "$INSTDIR\icon.ico"
  WriteRegStr HKCR "Directory\Background\shell\JetbrainLauncher" "Icon" "$INSTDIR\icon.ico"
  WriteRegStr HKCR "Directory\shell\JetbrainLauncher\command" "" '"$INSTDIR\run.bat" %V'
  WriteRegStr HKCR "Directory\Background\shell\JetbrainLauncher\command" "" '"$INSTDIR\run.bat" %V'
SectionEnd

Section -AdditionalIcons
  SetOutPath $INSTDIR
  CreateDirectory "$SMPROGRAMS\Jetbrains Launcher"
  CreateShortCut "$SMPROGRAMS\Jetbrains Launcher\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


; Uninstaller event handler functions.
Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK $(UNINST_DELETED)
FunctionEnd

Function un.myUnOnGuiInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 $(UNINST_DELETE_CONFIRM) IDYES +2
  Abort
FunctionEnd

Function un.onInit
  !insertmacro MUI_UNGETLANGUAGE
  ;MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 $(UNINST_DELETE_CONFIRM) IDYES +2
  ;Abort
FunctionEnd

Section Uninstall
  ; Delete files.
  Delete "$INSTDIR\uninst.exe"
  RMDir /r "$INSTDIR"

  Delete "$SMPROGRAMS\Jetbrains Launcher\Uninstall.lnk"

  RMDir "$SMPROGRAMS\Jetbrains Launcher"
  
  ; Delete registries.
  DeleteRegKey HKCR "Directory\shell\JetbrainLauncher"
  DeleteRegKey HKCR "Directory\Background\shell\JetbrainLauncher"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  SetAutoClose true
SectionEnd